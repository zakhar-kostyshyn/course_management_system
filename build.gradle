plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "co.uzzu.dotenv.gradle" version "1.2.0"
    id 'org.liquibase.gradle' version '2.1.0'
    id 'nu.studer.jooq' version '6.0.1'
    id "com.avast.gradle.docker-compose" version "0.16.4"
}


group = 'com.sombra'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'



configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:4.10.0'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.8'
    implementation 'com.auth0:java-jwt:4.0.0'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.10.0'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    liquibaseRuntime 'org.postgresql:postgresql'

    jooqGenerator 'org.postgresql:postgresql:42.3.2'
}

dockerCompose {
    database {
        useComposeFiles = ['docker-compose-build.yml']
    }
}

jooq {
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5433/cms_db'
                    user = 'root'
                    password = 'root'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        pojos = true
                        immutablePojos = false
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.sombra.promotion'
                        directory = 'build/generated-src/jooq/main'
                }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/liquibase/main_changelog.xml'
            url 'jdbc:postgresql://localhost:5433/cms_db'
            username 'root'
            password 'root'
        }
    }
}

update.dependsOn 'databaseComposeUp'
generateJooq.dependsOn update
